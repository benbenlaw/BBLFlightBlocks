import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.165'
    id 'net.darkhax.curseforgegradle' version '1.1.24'
    id "com.modrinth.minotaur" version "2.+"
}

version = "${minecraft_version}-${mod_version}"
group = mod_group_id

repositories {
    mavenLocal()

    maven {
        name = "Curse Maven"
        url = uri("https://cursemaven.com")
        content {
            includeGroup("curse.maven")
        }
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

runs {
    configureEach {
        systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }
    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

dependencies {

    implementation "net.neoforged:neoforge:${neo_version}"

    //BBL Core
    compileOnly "curse.maven:bbl-core-1135181:6486837"
    runtimeOnly "curse.maven:bbl-core-1135181:6486837"

    //FTB Essentials
    compileOnly "curse.maven:ftb-essentials-410811:5631477"
    runtimeOnly "curse.maven:ftb-essentials-410811:5631477"

    //Architectury
    compileOnly "curse.maven:architectury-api-419699:5786327"
    runtimeOnly "curse.maven:architectury-api-419699:5786327"

    //FTB Library
    compileOnly "curse.maven:ftb-library-forge-404465:5893689"
    runtimeOnly "curse.maven:ftb-library-forge-404465:5893689"

    //Modular Angel Ring
    compileOnly "curse.maven:modular-angel-ring-1037481:5893860"
    runtimeOnly "curse.maven:modular-angel-ring-1037481:5893860"

    //Curios Continuation
    compileOnly "curse.maven:curios-continuation-1037991:5888964"
    runtimeOnly "curse.maven:curios-continuation-1037991:5888964"

    //JEI
    compileOnly "curse.maven:jei-238222:5846880"
    runtimeOnly "curse.maven:jei-238222:5846880"
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {

        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }

        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}


tasks.register('publishCurseForge', TaskPublishCurseForge) {
    group "modpublishing"

    apiToken = System.getenv("curseforge_token").toString()

    def projectId = 1146799
    def mainFile = upload(projectId, jar)

    mainFile.changelog = changelog
    mainFile.addOptional("jei") // JEI
    mainFile.addRequirement("bbl-core")
    mainFile.releaseType = "release"
    mainFile.gameVersions.addAll("1.21", "1.21.1")
}

tasks.getByName("modrinth").setGroup("modpublishing")

modrinth {
    token = System.getenv("modrinth_token").toString()
    projectId = "Y1nt0bKX"
    versionType = "release"
    versionName = "bbl-flight-blocks-" + "$minecraft_version" + "-" + "$mod_version"
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!

    gameVersions.addAll("1.21", "1.21.1")

    loaders = ["neoforge"]
    changelog = project.changelog
    dependencies {
        optional.project "jei"
        required.project "bbl-core"
    }
}

